(define (make-withdraw initial-amount)
  (let ((balance initial-amount))
    (lambda (amount)
      (if (>= balance amount)
        (begin (set! balance (- balance amount))
               balance)
        "Insufficient funds"))))

;上の手続きを書き換え
(define (make-withdraw initial-amount)
  ((lambda (balance)
    (lambda (amount)
      (if (>= balance amount)
        (begin (set! balance (- balance amount))
               balance)
        "Insufficient funds")))
   initial-amount))

(define W1 (make-withdraw 100))

; まず、make-withdrawが呼び出され、フレームE1が作られる
;
;         #------------------------------------------#
; global->| make-withdraw: ...                       |
;         #------------------------------------------#
;                                      ^
;                                      |
;                               #--------------------#
;                           E1->| initial-amount:100 |
;                               #--------------------#
;                               ((lambda (balance)
;                                  (lambda (amount)
;                                    ...))
;                                initial-amount)

; make-withdrawの本体は、使い捨ての手続きオブジェクトを定義し、即座に呼び出している
; lambdaが評価され、E1の環境内で手続きオブジェクトが作られる
;
;         #------------------------------------------#
; global->| make-withdraw: ...                       |
;         #------------------------------------------#
;                                      ^
;                                      |
;                               #--------------------#
;                           E1->| initial-amount:100 |
;                               #--------------------#
;                                      ^
;                                      |
;                                   (* *)
;                                    |
;                                    V
;                       #------------------#
;                       | param:balance    |
;                       | (lambda (amount) |
;                       |   ...)           |
;                       #------------------#

; 手続きオブジェクトを即座に呼び出し、フレームE2が作られる
;
;         #------------------------------------------#
; global->| make-withdraw: ...                       |
;         #------------------------------------------#
;                                      ^
;                                      |
;                               #--------------------#
;                           E1->| initial-amount:100 |
;                               #--------------------#
;                                      ^         ^
;                                      |         |
;                                   (* *)        |
;                                    |           |
;                                    V           |
;                       #------------------#     |
;                       | param:balance    |     |
;                       | (lambda (amount) |     |
;                       |   ...)           |     |
;                       #------------------#     |
;                                                |
;                                         #-------------#
;                                     E2->| balance:100 |
;                                         #-------------#
;                                         (lambda (amount)
;                                           ...)

; lambdaが評価され、手続きオブジェクトを返し、defineでW1に束縛される
; E1内で作られた手続きオブジェクトは、他の部分からのポインタもないので無効となる
;
;         #------------------------------------------#
; global->| make-withdraw: ...                       |
;         | W1:*                                     |
;         #----|-------------------------------------#
;              |                       ^
;              |                       |
;              |                #--------------------#
;              |            E1->| initial-amount:100 |
;              |                #--------------------#
;              |                       ^
;              |                       |
;              |                #-------------#
;              |            E2->| balance:100 |
;              |                #-------------#
;              |                       ^
;              V                       |
;            (* *)---------------------#
;             |
;             V
;    #------------------------------#
;    | param: amount                |
;    | (if (>= balance amount) ...) |
;    #------------------------------#

(W1 50)

; W1が呼び出され、フレームE3が作られる
;
;         #------------------------------------------#
; global->| make-withdraw: ...                       |
;         | W1:*                                     |
;         #----|-------------------------------------#
;              |                       ^
;              |                       |
;              |                #--------------------#
;              |            E1->| initial-amount:100 |
;              |                #--------------------#
;              |                       ^
;              |                       |
;              |                #-------------#
;              |            E2->| balance:100 |
;              |                #-------------#
;              |                       ^   ^
;              V                       |   |
;            (* *)---------------------#   |
;             |                            |
;             V                            |
;    #------------------------------#      |
;    | param: amount                |      |
;    | (if (>= balance amount) ...) |      |
;    #------------------------------#      |
;                                          |
;                                 #-----------#
;                             E3->| amount:50 |
;                                 #-----------#
;                                 (if (>= balance amount)
;                                   ...)

; W1の本体が評価され、balanceが50に変更される
; E2内で作られたフレームは、他の部分からのポインタもないので無効となる
;
;         #------------------------------------------#
; global->| make-withdraw: ...                       |
;         | W1:*                                     |
;         #----|-------------------------------------#
;              |                       ^
;              |                       |
;              |                #--------------------#
;              |            E1->| initial-amount:100 |
;              |                #--------------------#
;              |                       ^
;              |                       |
;              |                #-------------#
;              |            E2->| balance:50  |
;              |                #-------------#
;              |                       ^
;              V                       |
;            (* *)---------------------#
;             |
;             V
;    #------------------------------#
;    | param: amount                |
;    | (if (>= balance amount) ...) |
;    #------------------------------#

(define W2 (make-withdraw 100))

; W1と独立した手続きオブジェクトが作られる
;
;         #---------------------------------------------------------#
; global->| make-withdraw: ...                                      |
;         | W1:*                              W2:*                  |
;         #----|---------------------------------|------------------#
;              |              ^                  |              ^
;              |              |                  |              |
;              |       #--------------------#    |       #--------------------#
;              |   E1->| initial-amount:100 |    |   E3->| initial-amount:100 |
;              |       #--------------------#    |       #--------------------#
;              |              ^                  |              ^
;              |              |                  |              |
;              |       #-------------#           |       #-------------#
;              |   E2->| balance:50  |           |   E4->| balance:100 |
;              |       #-------------#           |       #-------------#
;              |              ^                  |              ^
;              V              |                  V              |
;            (* *)------------#                (* *)------------#
;             |                                 |
;             V                                 |
;    #------------------------------#           |
;    | param: amount                |<----------#
;    | (if (>= balance amount) ...) |
;    #------------------------------#
